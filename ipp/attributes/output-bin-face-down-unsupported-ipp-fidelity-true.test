## test case to get supported output bin and define FACEDOWNOUTPUT
#############################################################
# Wait for printer to be ready
# Wait until job pool is clear
{
    NAME "Wait for printer to be ready"
    OPERATION Get-Jobs

    GROUP operation-attributes-tag
    ATTR charset attributes-charset utf-8
    ATTR naturalLanguage attributes-natural-language en
    ATTR uri printer-uri $uri
    ATTR name requesting-user-name $user
    ATTR keyword which-jobs not-completed

    STATUS successful-ok
    EXPECT !job-id REPEAT-NO-MATCH
}
####################################################################
# get output-bin-supported.  No status returned
{
        NAME "Step 2: Get supported output-bin."
        OPERATION Get-Printer-Attributes

        GROUP operation-attributes-tag
        ATTR charset attributes-charset utf-8
        ATTR language attributes-natural-language en
        ATTR uri printer-uri $uri
        ATTR keyword requested-attributes output-bin-supported

        EXPECT output-bin-supported OF-TYPE keyword IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE face-down DEFINE-MATCH FACEDOWNOUTPUT
}
## Test case to validate supported output bin works correctly 

#############################################################

##test case to validate output-bin-face-down with IPP Fidelity set to False specifically. It should print whether or not it is a supported output bin^M
{
	NAME "Step 3: Face-down value with IPP Fidelity=False"
	OPERATION Print-Job
	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri printer-uri $uri
	ATTR name requesting-user-name $user
	ATTR mimeMediaType document-format application/PCLm
	ATTR keyword compression "none"
	ATTR boolean ipp-attribute-fidelity false
	ATTR name document-name $filename

	GROUP job-attributes-tag
	ATTR keyword output-bin face-down

	FILE $filename
    
        STATUS successful-ok
	STATUS successful-ok-ignored-or-substituted-attributes

	EXPECT job-uri OF-TYPE uri COUNT 1 IN-GROUP job-attributes-tag
	EXPECT job-id OF-TYPE integer COUNT 1 IN-GROUP job-attributes-tag WITH-VALUE >0
	EXPECT job-state OF-TYPE enum COUNT 1 IN-GROUP job-attributes-tag WITH-VALUE 3,4,5,6,7,8,9
	EXPECT job-state-reasons OF-TYPE keyword IN-GROUP job-attributes-tag
	EXPECT ?job-state-message OF-TYPE text IN-GROUP job-attributes-tag
}

#############################################################
{
    NAME "   Step 4: Wait for job to finish"
    OPERATION Get-Jobs

    GROUP operation-attributes-tag
    ATTR charset attributes-charset utf-8
    ATTR naturalLanguage attributes-natural-language en
    ATTR uri printer-uri $uri
    ATTR name requesting-user-name $user
    ATTR keyword which-jobs not-completed

    #STATUS successful-ok
    EXPECT !job-id REPEAT-NO-MATCH
}
#############################################################

{	
    SKIP-IF-DEFINED FACEDOWNOUTPUT
	NAME "Step 5: Output Bin Face-down IPP fidelity True but face-down not supported"
	OPERATION Print-Job
	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri printer-uri $uri
	ATTR name requesting-user-name $user
	ATTR mimeMediaType document-format application/PCLm
	ATTR keyword compression "none"
	ATTR name job-name $filename
	ATTR boolean ipp-attribute-fidelity true	

	GROUP job-attributes-tag
	ATTR keyword output-bin face-down

	FILE $filename

	STATUS client-error-attributes-or-values-not-supported
	STATUS client-error-bad-request

	EXPECT !job-id
}
