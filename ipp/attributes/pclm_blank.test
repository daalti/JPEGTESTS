## Developer:   Thomas Griffiths
## Date:        1/7/2011
## Test:        Send any PCLm IPP print job
## Disclaimer:  File and Contents are HP property.

## USAGE:  ./ipptool -f filename -t printer-uri PCLm.test

IGNORE-ERRORS    no
VERSION          2.0
TRANSFER         auto
#############################################################
# Wait for printer to be ready
# Wait until job pool is clear
{
    NAME "Wait for printer to be ready"
    OPERATION Get-Jobs

    GROUP operation-attributes-tag
    ATTR charset attributes-charset utf-8
    ATTR naturalLanguage attributes-natural-language en
    ATTR uri printer-uri $uri
    ATTR name requesting-user-name $user
    ATTR keyword which-jobs not-completed

    STATUS successful-ok
    EXPECT !job-id REPEAT-NO-MATCH
}
#############################################################
#Removed Display call for printer-state-message
{
    NAME "Check IPP Server State"
    OPERATION Get-Printer-Attributes
    
	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri printer-uri $uri
    ATTR keyword requested-attributes printer-state
    ATTR keyword requested-attributes printer-is-accepting-jobs

    STATUS successful-ok
    EXPECT printer-is-accepting-jobs OF-TYPE boolean IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE true DEFINE-MATCH ACCEPTING   
    DISPLAY printer-is-accepting-jobs
    EXPECT printer-state OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 3 DEFINE-MATCH IDLESTATE
    DISPLAY printer-state
    DISPLAY printer-state-reasons
}

#Added ATTR name requesting-user-name $user since clients should provide this for Print-Job requests.
{
    SKIP-IF-NOT-DEFINED ACCEPTING
    SKIP-IF-NOT-DEFINED IDLESTATE
   
	NAME "PCLm file IPP Job Submission"
	OPERATION Print-Job

	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri printer-uri $uri
    ATTR name requesting-user-name $user
    ATTR mimeMediaType document-format application/PCLm
	ATTR name job-name $filename
	ATTR boolean ipp-attribute-fidelity true
	ATTR name document-name $filename

    ##TEST ASSUMES DEFAULT PAPER SIZE IS 8.5x11
    ##TEST ASSUMES DEFAULT PRINT RESOLUTION IS 600x600

	FILE $filename

	STATUS successful-ok
	EXPECT job-id
    DISPLAY job-id
    EXPECT job-uri
    DISPLAY job-uri
    EXPECT job-state
    DISPLAY job-state
    EXPECT job-state-reasons
    DISPLAY job-state-reasons
}
