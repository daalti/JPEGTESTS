## Developer:   Thomas Griffiths
## Date:        12/20/2010
## Test:        IPP print job Legal size
## Disclaimer:  File and Contents are HP property.

## USAGE:  ./ipptool -t -d 296=VAL -d 296=VAL -d 296=VAL -f /PATH/Legal_600_jpeg_32_RGB PRINTER-URI Legal_Bordered.test

IGNORE-ERRORS    no
VERSION          2.0
TRANSFER         auto
#############################################################
# Wait for printer to be ready
# Wait until job pool is clear
{
    NAME "Wait for printer to be ready"
    OPERATION Get-Jobs

    GROUP operation-attributes-tag
    ATTR charset attributes-charset utf-8
    ATTR naturalLanguage attributes-natural-language en
    ATTR uri printer-uri $uri
    ATTR name requesting-user-name $user
    ATTR keyword which-jobs not-completed

    STATUS successful-ok
    EXPECT !job-id REPEAT-NO-MATCH
}
#############################################################
{
    NAME "Check IPP Server State"
    OPERATION Get-Printer-Attributes
    
	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri printer-uri $uri

    ATTR keyword requested-attributes media-left-margin-supported
    ATTR keyword requested-attributes media-right-margin-supported
    ATTR keyword requested-attributes media-top-margin-supported
    ATTR keyword requested-attributes media-bottom-margin-supported

    ATTR keyword requested-attributes media-source-supported
    ATTR keyword requested-attributes printer-is-accepting-jobs
    ATTR keyword requested-attributes printer-state

    STATUS successful-ok

    EXPECT media-left-margin-supported WITH-VALUE $296 DEFINE-MATCH LEFT_MARGIN_SUPPORTED  
    EXPECT media-right-margin-supported WITH-VALUE $296 DEFINE-MATCH RIGHT_MARGIN_SUPPORTED
    EXPECT media-bottom-margin-supported WITH-VALUE $296 DEFINE-MATCH BOTTOM_MARGIN_SUPPORTED
    EXPECT media-top-margin-supported WITH-VALUE $296 DEFINE-MATCH TOP_MARGIN_SUPPORTED

    EXPECT media-source-supported OF-TYPE keyword WITH-VALUE "main" DEFINE-VALUE media_source
    EXPECT media-source-supported OF-TYPE keyword WITH-VALUE "auto" DEFINE-VALUE media_source

    EXPECT printer-is-accepting-jobs OF-TYPE boolean IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE true DEFINE-MATCH ACCEPTING
    EXPECT printer-state OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 3 DEFINE-MATCH IDLESTATE

    DISPLAY printer-is-accepting-jobs
    DISPLAY printer-state
    DISPLAY printer-state-reasons
    DISPLAY printer-state-message
}
#############################################################
## Test assumes the following:
## User will have loaded Legal plain/stationary media into the main tray
## If the device does not support the actual media col and margin values used, IPP Fidelity will cause job rejected.
{
    SKIP-IF-NOT-DEFINED ACCEPTING
    SKIP-IF-NOT-DEFINED IDLESTATE
    SKIP-IF-NOT-DEFINED TOP_MARGIN_SUPPORTED
    SKIP-IF-NOT-DEFINED RIGHT_MARGIN_SUPPORTED
    SKIP-IF-NOT-DEFINED BOTTOM_MARGIN_SUPPORTED
    SKIP-IF-NOT-DEFINED LEFT_MARGIN_SUPPORTED 

	NAME "Legal Bordered Margins"
	OPERATION Print-Job

	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri printer-uri $uri
	ATTR name job-name $filename
	ATTR boolean ipp-attribute-fidelity false
	ATTR name document-name $filename
    ATTR mimeMediaType document-format application/PCLm

    GROUP job-attributes-tag
    ATTR collection media-col {
	    MEMBER collection media-size {
        	#Legal    8.5x14in
	       MEMBER integer x-dimension 21590
               MEMBER integer y-dimension 35560       
            }
        #Paper Margins
        MEMBER integer media-top-margin $topmargin
		MEMBER integer media-bottom-margin $botmargin
		MEMBER integer media-left-margin $edgemargin
		MEMBER integer media-right-margin $edgemargin
		#Media Input Tray Source
        MEMBER keyword media-source $media_source
        
    }
         
	FILE $filename

	STATUS successful-ok
	EXPECT job-uri OF-TYPE uri COUNT 1 IN-GROUP job-attributes-tag
	EXPECT job-id OF-TYPE integer COUNT 1 IN-GROUP job-attributes-tag WITH-VALUE >0
	EXPECT job-state OF-TYPE enum COUNT 1 IN-GROUP job-attributes-tag WITH-VALUE 3,4,5,6,7,8,9
}
