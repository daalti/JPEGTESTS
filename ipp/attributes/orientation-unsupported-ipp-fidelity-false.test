## USAGE:  ./ipptool -f filename -t printer-uri orientation.test

## EXAMPLE: ./ipptool -v -f PCLm-file-within-specs -t ipp://10.0.1.24/ipp/print orientation.test

## UPDATED: 01/08/2015
## UPDATED: 6/4/2015:  Fix a typo, remove a not-recognized atttribute, and add some comments.

#############################################################
# Wait for printer to be ready
# Wait until job pool is clear
{
    NAME "Wait for printer to be ready"
    OPERATION Get-Jobs

    GROUP operation-attributes-tag
    ATTR charset attributes-charset utf-8
    ATTR naturalLanguage attributes-natural-language en
    ATTR uri printer-uri $uri
    ATTR name requesting-user-name $user
    ATTR keyword which-jobs not-completed

    STATUS successful-ok
    EXPECT !job-id REPEAT-NO-MATCH
}
#############################################################

#############################################################
# Substitution: Print with unsupported orientation, but allow printer to substitute or ignore the request.
# Print-Job with orientation-requested = 4 (Landscape), ipp-fidelity = False.  Expect successful-ok-ignored-or-substitut
{
	NAME "Step 6: Print 1 page document, unsupported orientation, fidelity=False"
	OPERATION Print-Job
	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri printer-uri $uri
	ATTR mimeMediaType document-format application/PCLm
	
	ATTR boolean ipp-attribute-fidelity false

    	GROUP job-attributes-tag
	ATTR enum orientation-requested 4

	FILE $filename
    
	STATUS successful-ok
	STATUS successful-ok-ignored-or-substituted-attributes
	EXPECT job-uri OF-TYPE uri COUNT 1 IN-GROUP job-attributes-tag
}

#############################################################
# Wait until job queue clears
{
    NAME "	Step 7: Wait for printer to finish"
    OPERATION Get-Jobs

    GROUP operation-attributes-tag
    ATTR charset attributes-charset utf-8
    ATTR naturalLanguage attributes-natural-language en
    ATTR uri printer-uri $uri
    ATTR name requesting-user-name $user
    ATTR keyword which-jobs not-completed

    STATUS successful-ok
    EXPECT !job-id REPEAT-NO-MATCH
}
#############################################################
# get job-attributes.  Check that orientation-requested=4
{
	NAME "   Step 8: Check orientation-requested==4"
	OPERATION Get-Job-Attributes
	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri printer-uri $uri
	ATTR integer job-id $job-id
	ATTR keyword requested-attributes orientation-requested
    
	STATUS successful-ok
	DISPLAY job-uri	
	
	#CHANGE the line below FOR 2012 Products and earlier - this would not be the correct expectation. Will need to get more information.	
	EXPECT orientation-requested OF-TYPE enum IN-GROUP job-attributes-tag WITH-VALUE 4
}
#############################################################

## Notes for orientation-requested:
## Value of 3 is Portrait
## Value of 4 is Landscape
## Value of 5 is reverse-landscape
## Value of 6 is reverse-portrait
