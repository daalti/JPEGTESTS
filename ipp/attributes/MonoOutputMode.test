## Developer:   Thomas Griffiths
## Date:        12-17-2010
## Test:        Tests a Color image printed with monochrome output mode set.
## Disclaimer:  File and Contents are HP property.

IGNORE-ERRORS    no
VERSION          2.0
TRANSFER         auto
#############################################################
# Wait for printer to be ready
# Wait until job pool is clear
{
    NAME "Wait for printer to be ready"
    OPERATION Get-Jobs

    GROUP operation-attributes-tag
    ATTR charset attributes-charset utf-8
    ATTR naturalLanguage attributes-natural-language en
    ATTR uri printer-uri $uri
    ATTR name requesting-user-name $user
    ATTR keyword which-jobs not-completed

    STATUS successful-ok
    EXPECT !job-id REPEAT-NO-MATCH
}
#############################################################
{
    NAME "Check IPP Server State"
    OPERATION Get-Printer-Attributes
    
	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri printer-uri $uri
    ATTR keyword requested-attributes printer-state
    ATTR keyword requested-attributes printer-is-accepting-jobs
	ATTR keyword requested-attributes output-mode-supported

    STATUS successful-ok
   EXPECT output-mode-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE "monochrome" DEFINE-MATCH MONOCHROME
    EXPECT printer-is-accepting-jobs OF-TYPE boolean IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE true DEFINE-MATCH ACCEPTING   
    DISPLAY printer-is-accepting-jobs
    EXPECT printer-state OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 3 DEFINE-MATCH IDLESTATE
    DISPLAY printer-state
}

{
    SKIP-IF-NOT-DEFINED MONOCHROME
    SKIP-IF-NOT-DEFINED ACCEPTING
    SKIP-IF-NOT-DEFINED IDLESTATE

	NAME "Color source file with Monochrome Output Mode"
	OPERATION Print-Job
	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
 	ATTR naturalLanguage attributes-natural-language en
	ATTR uri printer-uri $uri
    ATTR mimeMediaType document-format application/PCLm
	ATTR name job-name $filename
	ATTR boolean ipp-attribute-fidelity false
	ATTR name document-name $filename

    ##TEST ASSUMES DEFAULT PAPER SIZE IS 8.5x11
    ##TEST ASSUMES DEFAULT PRINT RESOLUTION IS 600x600

    GROUP job-attributes-tag
	ATTR keyword print-color-mode monochrome

	FILE $filename
    
	STATUS successful-ok
	EXPECT job-uri OF-TYPE uri COUNT 1 IN-GROUP job-attributes-tag
	EXPECT job-id OF-TYPE integer COUNT 1 IN-GROUP job-attributes-tag WITH-VALUE >0
    DISPLAY job-id
	EXPECT job-state OF-TYPE enum COUNT 1 IN-GROUP job-attributes-tag WITH-VALUE 3,4,5,6,7,8,9
    DISPLAY job-state
    EXPECT job-state-reasons OF-TYPE keyword IN-GROUP job-attributes-tag
    DISPLAY job-state-reasons
}
