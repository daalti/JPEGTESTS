## Developer:   Thomas Griffiths
## Date:        12/10/2010
## Test:        IPP Long Edge Duplex Printing
## Disclaimer:  File and Contents are HP property.

## USAGE:  ./ipptool -f filename -t printer-uri PCLm_Duplex_Long.test
#############################################################
# Wait for printer to be ready
# Wait until job pool is clear
{
    NAME "Wait for printer to be ready"
    OPERATION Get-Jobs

    GROUP operation-attributes-tag
    ATTR charset attributes-charset utf-8
    ATTR naturalLanguage attributes-natural-language en
    ATTR uri printer-uri $uri
    ATTR name requesting-user-name $user
    ATTR keyword which-jobs not-completed

    STATUS successful-ok
    EXPECT !job-id REPEAT-NO-MATCH
}
#############################################################
{
    NAME "Check IPP Server Printing State"
    OPERATION Get-Printer-Attributes
    
	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri printer-uri $uri
    ATTR keyword requested-attributes printer-state
    ATTR keyword requested-attributes printer-is-accepting-jobs
    ATTR keyword requested-attributes sides-supported

    STATUS successful-ok
    EXPECT sides-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE "two-sided-long-edge" DEFINE-MATCH LONGEDGE   
    DISPLAY sides-supported
    EXPECT printer-is-accepting-jobs OF-TYPE boolean IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE true DEFINE-MATCH ACCEPTING   
    DISPLAY printer-is-accepting-jobs
    EXPECT printer-state OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 3 DEFINE-MATCH IDLESTATE
    DISPLAY printer-state
}

{
    SKIP-IF-NOT-DEFINED LONGEDGE
    SKIP-IF-NOT-DEFINED ACCEPTING
    SKIP-IF-NOT-DEFINED IDLESTATE

	NAME "A multipage duplex job using long-edge"
	OPERATION Print-Job
	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri printer-uri $uri
    ATTR mimeMediaType document-format application/PCLm
	ATTR name job-name $filename
	ATTR boolean ipp-attribute-fidelity true
	ATTR name document-name $filename

    GROUP job-attributes-tag
    ATTR keyword sides "two-sided-long-edge"

    ##TEST ASSUMES DEFAULT PAPER SIZE IS 8.5x11
    ##TEST ASSUMES DEFAULT PRINT RESOLUTION IS 600x600

	FILE $filename

	STATUS successful-ok
   	EXPECT job-uri OF-TYPE uri COUNT 1 IN-GROUP job-attributes-tag
    DISPLAY job-uri
	EXPECT job-id OF-TYPE integer COUNT 1 IN-GROUP job-attributes-tag WITH-VALUE >0
    DISPLAY job-id
}
