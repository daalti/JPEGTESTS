#   Copyright 2014-2015 by Apple Inc.
#
#   These coded instructions, statements, and computer programs are the
#   property of Apple Inc. and are protected by Federal copyright
#   law.

  # The version to use
	VERSION 2.0

{
	NAME "Get print-color-mode-supported attribute value"
	OPERATION get-printer-attributes
	GROUP operation
	ATTR charset attributes-charset utf-8
	ATTR language attributes-natural-language en
	ATTR uri printer-uri $uri
	ATTR name requesting-user-name $user
	ATTR keyword requested-attributes print-color-mode-supported

	STATUS successful-ok

	# If color is supported, then will send a job with color, otherwise monochrome
	# Since color is priority, update the value of PRINT_COLOR_MODE at the end.
	EXPECT print-color-mode-supported WITH-VALUE monochrome DEFINE-VALUE PRINT_COLOR_MODE
	EXPECT print-color-mode-supported WITH-VALUE color DEFINE-VALUE PRINT_COLOR_MODE
}

# Create Job
{
	# Skip the test if both color & monochrome are not supported
	SKIP-IF-NOT-DEFINED PRINT_COLOR_MODE

	# The name of the test...
	NAME "Create-Job: 600x600dpi RGB, Normal, Letter, no media type specified, auto"

	# The resource to use for the POST
	# RESOURCE /admin

	# The operation to use
	OPERATION create-job

	# Defines that may be passed to ipptest's -d option:
	#	PRINTER_TAKES_JPEG

	# Attributes, starting in the operation group...
	GROUP operation
	ATTR charset attributes-charset utf-8
	ATTR language attributes-natural-language en
	ATTR uri printer-uri $uri
	ATTR name requesting-user-name $user
	ATTR boolean ipp-attribute-fidelity true

	GROUP job
	ATTR integer copies 2
	ATTR resolution printer-resolution 600x600dpi
	ATTR enum print-quality 4
	ATTR keyword print-color-mode $PRINT_COLOR_MODE
	ATTR keyword print-scaling auto
	ATTR collection media-col { 
		MEMBER keyword media-source auto
		MEMBER collection media-size {
			MEMBER integer x-dimension 21590
			MEMBER integer y-dimension 27940
		}
		MEMBER integer media-bottom-margin 423
		MEMBER integer media-left-margin 423
		MEMBER integer media-right-margin 423
		MEMBER integer media-top-margin 423
	}
	# What statuses are OK?
	STATUS successful-ok
		DEFINE-MATCH JOB_CREATED_1
	STATUS successful-ok
	STATUS server-error-busy REPEAT-MATCH
	STATUS server-error-too-many-jobs REPEAT-MATCH
	STATUS server-error-not-accepting-jobs REPEAT-MATCH
	STATUS client-error-conflicting-attributes
	STATUS client-error-attributes-or-values-not-supported
	# What attributes do we expect?
	EXPECT job-id
		IF-DEFINED JOB_CREATED_1
	EXPECT job-uri
		IF-DEFINED JOB_CREATED_1
}
# Send-document
{
	# The name of the test...
	NAME "Print JPEG file using Send-Document: 600x600dpi RGB, Normal, Letter, no media type specified, auto"

	SKIP-IF-NOT-DEFINED JOB_CREATED_1
	# The resource to use for the POST
	# RESOURCE /admin

	# The operation to use
	OPERATION send-document

	# Defines that may be passed to ipptest's -d option:
	#	PRINTER_TAKES_JPEG

	# Attributes, starting in the operation group...
	GROUP operation
	ATTR charset attributes-charset utf-8
	ATTR language attributes-natural-language en
	ATTR uri printer-uri $uri
	ATTR integer job-id $job-id
	ATTR boolean last-document true
	ATTR name requesting-user-name $user
	ATTR mimetype document-format image/jpeg
	ATTR keyword compression none

	FILE $filename

	# What statuses are OK?
	STATUS successful-ok
		IF-DEFINED PRINTER_TAKES_JPEG
	STATUS successful-ok
		DEFINE-MATCH JOB_CREATED_2
	STATUS client-error-conflicting-attributes
	STATUS client-error-document-format-not-supported
		IF-NOT-DEFINED PRINTER_TAKES_JPEG

	# What attributes do we expect?
	STATUS server-error-busy REPEAT-MATCH
	STATUS server-error-not-accepting-jobs REPEAT-MATCH
	EXPECT job-id
		IF-DEFINED JOB_CREATED_2
	EXPECT job-uri
		IF-DEFINED JOB_CREATED_2
}